صنف مـناسبة {
    عرف الاسم: نـص؛
    عرف اليوم_الهجري: صـحيح؛
    عرف الشهر_الهجري: صـحيح؛
    عرف السنة_الهجرية: صـحيح؛
    عرف اليوم_الميلادي: صـحيح؛
    عرف الشهر_الميلادي: صـحيح؛
    عرف السنة_الميلادية: صـحيح؛
    عرف الأيام_المتبقية: صـحيح؛
    عملية هذا~هيئ() {}
    نـبم.مدير_شبم.احشر_عمليات_النسخ[]؛
}

دالة هات_المناسبات_الهجرية_القادمة (تاريخ_هجري: هـجري.تـاريخ): مـصفوفة[مـناسبة] {
    عرف تاريخ_ميلادي: هـجري.تـاريخ = هـجري.حول_لميلادي(تاريخ_هجري.السنة، تاريخ_هجري.الشهر، تاريخ_هجري.اليوم)؛

    عرف المناسبات: مـصفوفة[مـناسبة]({
        مـناسبة().{ اليوم_الهجري = 1؛ الشهر_الهجري = 1؛ الاسم = "رأس السنة الهجرية" }،
        مـناسبة().{ اليوم_الهجري = 10؛ الشهر_الهجري = 1؛ الاسم = "عاشوراء" }،
        مـناسبة().{ اليوم_الهجري = 1؛ الشهر_الهجري = 9؛ الاسم = "بداية رمضان" }،
        مـناسبة().{ اليوم_الهجري = 1؛ الشهر_الهجري = 10؛ الاسم = "عيد الفطر" }،
        مـناسبة().{ اليوم_الهجري = 1؛ الشهر_الهجري = 12؛ الاسم = "الأول من ذي الحجة" }،
        مـناسبة().{ اليوم_الهجري = 9؛ الشهر_الهجري = 12؛ الاسم = "يوم عرفة" }،
        مـناسبة().{ اليوم_الهجري = 10؛ الشهر_الهجري = 12؛ الاسم = "عيد الأضحى" }،
    })؛

    // حساب كل مناسبة
    عرف ع: صـحيح؛
    لكل ع = 0، ع < المناسبات.هات_الطول()، ++ع {
        // تجربة السنة الحالية
        عرف تاريخ_م: هـجري.تـاريخ = هـجري.حول_لميلادي(
            تاريخ_هجري.السنة،
            المناسبات(ع).الشهر_الهجري،
            المناسبات(ع).اليوم_الهجري
        )؛
        عرف فرق: صـحيح = احسب_فرق_الايام(
            تاريخ_ميلادي.السنة، تاريخ_ميلادي.الشهر، تاريخ_ميلادي.اليوم،
            تاريخ_م.السنة، تاريخ_م.الشهر، تاريخ_م.اليوم
        )؛

        // إذا مضت المناسبة، استخدم السنة القادمة
        إذا فرق < 0 {
            تاريخ_م = هـجري.حول_لميلادي(تاريخ_هجري.السنة + 1، المناسبات(ع).الشهر_الهجري، المناسبات(ع).اليوم_الهجري)؛
            ++المناسبات(ع).السنة_الهجرية؛
            فرق = احسب_فرق_الايام(
            تاريخ_ميلادي.السنة، تاريخ_ميلادي.الشهر، تاريخ_ميلادي.اليوم،
                تاريخ_م.السنة، تاريخ_م.الشهر، تاريخ_م.اليوم
            )؛
            المناسبات(ع).السنة_الهجرية = تاريخ_هجري.السنة + 1؛
        } وإلا {
            المناسبات(ع).السنة_الهجرية = تاريخ_هجري.السنة؛
        }

        المناسبات(ع).اليوم_الميلادي = تاريخ_م.اليوم؛
        المناسبات(ع).الشهر_الميلادي = تاريخ_م.الشهر؛
        المناسبات(ع).السنة_الميلادية = تاريخ_م.السنة؛
        المناسبات(ع).الأيام_المتبقية = فرق؛
    }

    // ترتيب المناسبات حسب الأيام المتبقية (الأقرب أولاً)
    لكل ع = 0، ع < المناسبات.هات_الطول() - 1، ++ع {
        عرف ك: صـحيح؛
        لكل ك = ع + 1، ك < المناسبات.هات_الطول()، ++ك {
            إذا المناسبات(ك).الأيام_المتبقية < المناسبات(ع).الأيام_المتبقية {
                عرف مؤقت: مـناسبة = المناسبات(ع)؛
                المناسبات(ع) = المناسبات(ك)؛
                المناسبات(ك) = مؤقت؛
            }
        }
    }

    أرجع المناسبات؛
}

// حساب Julian Day Number للتاريخ الميلادي
دالة احسب_جوليان(السنة: صـحيح، الشهر: صـحيح، اليوم: صـحيح): صـحيح {
    عرف أ: صـحيح = (14 - الشهر) / 12؛
    عرف ي: صـحيح = السنة + 4800 - أ؛
    عرف م: صـحيح = الشهر + 12 * أ - 3؛
    أرجع اليوم + (153 * م + 2) / 5 + 365 * ي + ي / 4 - ي / 100 + ي / 400 - 32045؛
}

// حساب الفرق بالأيام بين تاريخين ميلاديين
دالة احسب_فرق_الايام(سنة1: صـحيح، شهر1: صـحيح، يوم1: صـحيح، سنة2: صـحيح، شهر2: صـحيح، يوم2: صـحيح): صـحيح {
    أرجع احسب_جوليان(سنة2، شهر2، يوم2) - احسب_جوليان(سنة1، شهر1، يوم1)؛
}

دالة احسب_يوم_الاسبوع(السنة_الميلادية: صـحيح، الشهر_الميلادي: صـحيح، اليوم_الميلادي: صـحيح): نـص {
    // حساب يوم الأسبوع باستخدام خوارزمية Sakamoto
    عرف جدول_الأشهر: مـصفوفة[صـحيح]({ 0، 3، 2، 5، 0، 3، 5، 1، 4، 6، 2، 4 })؛
    إذا الشهر_الميلادي < 3 السنة_الميلادية = السنة_الميلادية - 1؛
    عرف يوم_الأسبوع: صـحيح = (
        السنة_الميلادية +
        السنة_الميلادية ÷ 4 -
        السنة_الميلادية ÷ 100 +
        السنة_الميلادية ÷ 400 +
        جدول_الأشهر(الشهر_الميلادي - 1) +
        اليوم_الميلادي
    ) % 7؛

    // أسماء أيام الأسبوع
    عرف أيام_الأسبوع: مـصفوفة[مـؤشر_محارف]({
        "الأحد"، "الاثنين"، "الثلاثاء"، "الأربعاء"،
        "الخميس"، "الجمعة"، "السبت"
    })؛
    أرجع نـص(أيام_الأسبوع(يوم_الأسبوع))؛
}
