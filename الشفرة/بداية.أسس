اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/WebPlatform"، "مـنصة_ويب.أسس")؛
مـحا.اشمل_ملف("Alusus/Http"، "بـننف.أسس")؛
مـحا.اشمل_ملف("Alusus/Ummulqura"، "هـجري.أسس")؛
مـحا.اشمل_ملف("Alusus/Nashir"، { "نـاشر.أسس"، "مـشغلات_نشر/الـأسس_نت.أسس" })؛
مـحا.اشمل_ملف("Alusus/ProgArg"، "بـروجارج.أسس")؛
مـحا.اشمل_ملف("Alusus/Json"، "جـيسون.أسس")؛
اشمل "مـتم/نـص"؛
اشمل "مـتم/طـرفية"؛
اشمل "مـتم/مـصفوفة"؛
اشمل "مـتم/سندات"؛
اشمل "مـتم/شـبكة"؛
اشمل "مـتم/ذاكـرة"؛

استخدم مـتم؛
استخدم مـنصة_ويب؛

اشمل "حسابات"؛
اشمل "صـندوق_مناسبة"؛
اشمل "نـافذة_أوقات_الصلاة"؛
اشمل "الـصفحة_الرئيسية"؛
اشمل "منفذ_أوقات_الصلاة"؛

//=============================================================================
// متغيرات عمومية

// المكداس الرئيسي للتطبيق
عرف المكداس_الرئيسي: سـندنا[مـكداس]؛

// أسماء الأشهر الهجرية
عرف الأشهر_الهجرية: مـصفوفة[مـؤشر_محارف]({
    "محرم"، "صفر"، "ربيع الأول"، "ربيع الآخر"،
    "جمادى الأولى"، "جمادى الآخرة"، "رجب"، "شعبان"،
    "رمضان"، "شوال"، "ذو القعدة"، "ذو الحجة"
})؛

// أسماء الأشهر الميلادية
عرف الأشهر_الميلادية: مـصفوفة[مـؤشر_محارف]({
    "يناير"، "فبراير"، "مارس"، "أبريل"، "مايو"، "يونيو"،
    "يوليو"، "أغسطس"، "سبتمبر"، "أكتوبر"، "نوفمبر"، "ديسمبر"
})؛

//=============================================================================
// واجهة التطبيق

@مسار_موارد["/"] عرف مسار_الموارد: "الموارد/"؛

@منفذ_مرئي["/"]
@عنوان["هجري - التقويم الهجري"]
@تطبيق_ويب["الموارد/manifest.json"، "إصدار4"]
@أيقونة["الموارد/hijri_icon_64.png"]
@خزن_مسبق["/"]
@خزن_تفاعلي["^/"]
دالة الرئيسية {
    حمل_خط("Tajawal", "/Tajawal-Medium.ttf");
    حمل_خط("TajawalBold", "/Tajawal-Bold.ttf");

    نـافذة.النموذج.الطراز.{
        الحشوة = مـسافة4.بكسلات(0)؛
        الهامش = مـسافة4.بكسلات(0)؛
        أدنى_طول = مـسافة.مئوي(100)؛
        الخلفية = خـلفية(لـون("E0E0F5"))؛
        فئة_الخط = "Tajawal"؛
        الاتجاه = اتـجاه._من_اليمين_؛
    }؛

    نـافذة.النموذج.حدد_المشهد(مـكداس().{
        المكداس_الرئيسي = هذا؛
        الطراز.{
            العرض = مـسافة.مئوي(100)؛
            الطول = مـسافة.مئوي(100)؛
        }؛
        حدد_الانتقال(نـص("للأمام"), أنشئ_انتقال_تلاشي_مع_تحجيم_مكداس(0.3, 0.5، 0))؛
        حدد_الانتقال(نـص("للخلف"), أنشئ_انتقال_تلاشي_مع_تحجيم_مكداس(0.3, 0.5، 1))؛
        أضف(الـصفحة_الرئيسية())؛
    })؛

    نفذ_حلقة_معالجة_الأحداث()؛
}

//=============================================================================
// بدء الخادم

نـاشر.إطناب = 0؛
نـاشر.اضبط[نـاشر.إعـدادات().{
    اسم_المشروع = "هجري"؛
    رقم_الإصدار = "v1"؛
    منفذ_الخادم = 8000؛
    مشغل_النشر = نـاشر.مـشغل_نشر_الأسس_نت(نـص("hijri"))؛
}]؛
بـروجارج.حلل(2)؛
