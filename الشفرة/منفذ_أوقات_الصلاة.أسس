// منفذ بياني لجلب أوقات الصلاة من aladhan.com
@منفذ_بياني["GET", "/api/prayer-times"]
دالة هات_أوقات_الصلاة(اتصال: مؤشر[بـننف.اتـصال]) {
    عرف معلومات_الطلب: مؤشر[بـننف.مـعلومات_الطلب] = بـننف.هات_معلومات_الطلب(اتصال)؛
    // قراءة معاملات الطلب
    عرف يوم: نـص؛
    عرف شهر: نـص؛
    عرف سنة: نـص؛

    // استخراج المعاملات من query string
    عرف المعاملات: نـص = نـص(معلومات_الطلب~محتوى.نص_الاستعلام)؛

    // تحليل المعاملات
    عرف أجزاء_الاستعلام: مـصفوفة[نـص] = المعاملات.قطع("&")؛
    عرف ع: صـحيح؛
    لكل ع = 0، ع < أجزاء_الاستعلام.هات_الطول()، ++ع {
        عرف جزء: نـص = أجزاء_الاستعلام(ع)؛
        عرف أجزاء_المعامل: مـصفوفة[نـص] = جزء.قطع("=")؛
        إذا أجزاء_المعامل.هات_الطول() >= 2 {
            إذا أجزاء_المعامل(0) == "day" {
                يوم = أجزاء_المعامل(1)؛
            } وإلا إذا أجزاء_المعامل(0) == "month" {
                شهر = أجزاء_المعامل(1)؛
            } وإلا إذا أجزاء_المعامل(0) == "year" {
                سنة = أجزاء_المعامل(1)؛
            }
        }
    }

    // التحقق من وجود المعاملات المطلوبة
    إذا يوم.هات_الطول() == 0 أو شهر.هات_الطول() == 0 أو سنة.هات_الطول() == 0 {
        بـننف.اطبع(اتصال، "HTTP/1.1 400 Bad Request\r\n")؛
        بـننف.اطبع(اتصال، "Content-Type: application/json; charset=utf-8\r\n\r\n")؛
        بـننف.اطبع(اتصال، "{\"error\":\"Missing required parameters: day, month, year\"}")؛
        أرجع؛
    }

    عرف المدينة: نـص؛
    عرف البلد: نـص؛
    اجلب_العنوان_الجغرافي(هات_عنوان_المستخدم(اتصال)، المدينة، البلد)؛

    عرف الرد: نـص = اجلب_أوقات_الصلاة(المدينة، البلد، يوم، شهر، سنة)؛

    إذا الرد != "" {
        // إرجاع أوقات الصلاة
        بـننف.اطبع(اتصال، "HTTP/1.1 200 OK\r\n")؛
        بـننف.اطبع(اتصال، "Content-Type: application/json; charset=utf-8\r\n")؛
        بـننف.اطبع(اتصال، "Access-Control-Allow-Origin: *\r\n")؛
        بـننف.اطبع(اتصال، "Content-Length: %d\r\n\r\n", الرد.هات_الطول())؛
        بـننف.اطبع(اتصال، "%s", الرد.صوان)؛
    } وإلا {
        // خطأ في جلب البيانات
        بـننف.اطبع(اتصال، "HTTP/1.1 500 Internal Server Error\r\n")؛
        بـننف.اطبع(اتصال، "Content-Type: application/json; charset=utf-8\r\n\r\n")؛
        بـننف.اطبع(اتصال، "{\"error\":\"Failed to fetch prayer times from external API\"}")؛
    }
}

دالة هات_عنوان_المستخدم (اتصال: مؤشر[بـننف.اتـصال]): نـص {
    عرف معلومات_الطلب: مؤشر[بـننف.مـعلومات_الطلب] = بـننف.هات_معلومات_الطلب(اتصال)؛
    عرف عنوان_آيبي: نـص = نـص(بـننف.هات_الترويسة(اتصال، "X-Forwarded-For")).قطع(",")(0).شذب()؛
    إذا عنوان_آيبي == "" عنوان_آيبي = نـص(معلومات_الطلب~محتوى.العنوان_البعيد~مؤشر)؛
    // إذا كان IP فارغاً أو localhost، استخدم IP عام للاختبار
    إذا عنوان_آيبي.هات_الطول() == 0 أو عنوان_آيبي == "127.0.0.1" أو عنوان_آيبي == "::1" {
        عنوان_آيبي = "8.8.8.8"؛ // IP عام للاختبار
    }
    أرجع عنوان_آيبي؛
}

دالة اجلب_العنوان_الجغرافي(عنوان_آيبي: نـص، المدينة: سند[نـص]، البلد: سند[نـص]) {
    // جلب موقع المستخدم من ip-api.com
    عرف رابط_IP: نـص = نـص.املأ("http://ip-api.com/json/%s?fields=city,country", عنوان_آيبي.صوان)؛
    عرف بيانات_الموقع: مـؤشر_محارف؛
    عرف حجم_الموقع: صـحيح؛

    عرف نتيجة_الموقع: صـحيح = شـبكة.هات(رابط_IP.صوان، بيانات_الموقع~مؤشر، حجم_الموقع~مؤشر)؛

    // قيم افتراضية للمدينة والبلد
    المدينة = "Makkah"؛
    البلد = "Saudi Arabia"؛

    // استخراج المدينة والبلد من استجابة ip-api
    إذا نتيجة_الموقع == 1 && حجم_الموقع > 0 {
        عرف موقع_جيسون: جـيسون = نـص(بيانات_الموقع)؛
        المدينة = موقع_جيسون("city")~مثل[نـص]؛
        البلد = موقع_جيسون("country")~مثل[نـص]؛
        ذاكـرة.حرر(بيانات_الموقع)؛
    }
}

دالة اجلب_أوقات_الصلاة(المدينة: نـص، البلد: نـص، يوم: نـص، شهر: نـص، سنة: نـص): نـص {
    // ترميز المدينة والبلد للاستخدام في URL
    عرف المدينة_المرمزة: نـص = شـبكة.شفر_عنوانيا(المدينة.صوان)؛
    عرف البلد_المرمز: نـص = شـبكة.شفر_عنوانيا(البلد.صوان)؛

    // بناء رابط API
    عرف التاريخ: نـص = نـص.املأ("%s-%s-%s", يوم.صوان، شهر.صوان، سنة.صوان)؛
    عرف الرابط: نـص = نـص.املأ(
        ‏"https://api.aladhan.com/v1/timingsByCity/%s?city=%s&country=%s&method=4"،
        التاريخ.صوان، المدينة_المرمزة.صوان، البلد_المرمز.صوان
    )؛

    // جلب البيانات من API الخارجي
    عرف البيانات: مـؤشر_محارف؛
    عرف الحجم: صـحيح؛

    عرف النتيجة: صـحيح = شـبكة.هات(الرابط.صوان، البيانات~مؤشر، الحجم~مؤشر)؛

    إذا النتيجة == 1 && الحجم > 0 {
        // قراءة وتحليل البيانات باستخدام مكتبة Json
        عرف كائن_جيسون: جـيسون = نـص(البيانات)؛

        // استخراج أوقات الصلاة من data.timings
        عرف الفجر: نـص = كائن_جيسون("data")("timings")("Fajr")؛
        عرف الشروق: نـص = كائن_جيسون("data")("timings")("Sunrise")؛
        عرف الظهر: نـص = كائن_جيسون("data")("timings")("Dhuhr")؛
        عرف العصر: نـص = كائن_جيسون("data")("timings")("Asr")؛
        عرف المغرب: نـص = كائن_جيسون("data")("timings")("Maghrib")؛
        عرف العشاء: نـص = كائن_جيسون("data")("timings")("Isha")؛

        ذاكـرة.حرر(البيانات)؛

        // بناء رد JSON يحتوي على أوقات الصلاة والموقع
        أرجع نـص.املأ(
            ‏"{\"fajr\":\"%s\",\"sunrise\":\"%s\",\"dhuhr\":\"%s\",\"asr\":\"%s\",\"maghrib\":\"%s\",\"isha\":\"%s\",\"city\":\"%s\",\"country\":\"%s\"}"،
            الفجر.صوان، الشروق.صوان، الظهر.صوان، العصر.صوان، المغرب.صوان، العشاء.صوان، المدينة.صوان، البلد.صوان
        )؛
    } وإلا {
        أرجع نـص()؛
    }
}
