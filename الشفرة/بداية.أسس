اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/WebPlatform"، "مـنصة_ويب.أسس")؛
مـحا.اشمل_ملف("Alusus/Ummulqura"، "هـجري.أسس")؛
مـحا.اشمل_ملف("Alusus/Nashir"، { "نـاشر.أسس"، "مـشغلات_نشر/الـأسس_نت.أسس" })؛
مـحا.اشمل_ملف("Alusus/ProgArg"، "بـروجارج.أسس")؛
اشمل "مـتم/نـص"؛
اشمل "مـتم/طـرفية"؛
اشمل "مـتم/مـصفوفة"؛
اشمل "مـتم/سندات"؛

استخدم مـتم؛
استخدم مـنصة_ويب؛

اشمل "حسابات"؛
اشمل "صـندوق_مناسبة"؛

//=============================================================================
// متغيرات عمومية

// أسماء الأشهر الهجرية
عرف الأشهر_الهجرية: مـصفوفة[مـؤشر_محارف]({
    "محرم"، "صفر"، "ربيع الأول"، "ربيع الآخر"،
    "جمادى الأولى"، "جمادى الآخرة"، "رجب"، "شعبان"،
    "رمضان"، "شوال"، "ذو القعدة"، "ذو الحجة"
})؛

// أسماء الأشهر الميلادية
عرف الأشهر_الميلادية: مـصفوفة[مـؤشر_محارف]({
    "يناير"، "فبراير"، "مارس"، "أبريل"، "مايو"، "يونيو"،
    "يوليو"، "أغسطس"، "سبتمبر"، "أكتوبر"، "نوفمبر"، "ديسمبر"
})؛

//=============================================================================
// واجهة التطبيق

@مسار_موارد["/"] عرف مسار_الموارد: "الموارد/"؛

@منفذ_مرئي["/"]
@عنوان["هجري - التقويم الهجري"]
@تطبيق_ويب["الموارد/manifest.json"، "إصدار3"]
@أيقونة["الموارد/hijri_icon_64.png"]
@خزن_مسبق["/"]
@خزن_تفاعلي["^/"]
دالة الرئيسية {
    حمل_خط("Tajawal", "/Tajawal-Medium.ttf");
    حمل_خط("TajawalBold", "/Tajawal-Bold.ttf");

    // الحصول على التاريخ الحالي
    عرف التاريخ: نـص = هات_التاريخ("local_iso"، هات_الختم_الزمني())؛
    عرف أجزاء_التاريخ: مـصفوفة[نـص] = التاريخ.قطع(" ")(0).قطع("-")؛

    عرف السنة_الميلادية: صـحيح = نـص.اقرأ_صحيح(أجزاء_التاريخ(0))؛
    عرف الشهر_الميلادي: صـحيح = نـص.اقرأ_صحيح(أجزاء_التاريخ(1))؛
    عرف اليوم_الميلادي: صـحيح = نـص.اقرأ_صحيح(أجزاء_التاريخ(2))؛

    عرف يوم_الأسبوع: نـص = احسب_يوم_الاسبوع(السنة_الميلادية، الشهر_الميلادي، اليوم_الميلادي)؛

    // التحويل إلى هجري
    عرف تاريخ_هجري: هـجري.تـاريخ = هـجري.حول_لهجري(
        السنة_الميلادية،
        الشهر_الميلادي،
        اليوم_الميلادي
    )؛

    // تجهيز النصوص
    عرف نص_ميلادي: نـص؛
    نص_ميلادي = نـص.املأ("%i %s %i م"،
        اليوم_الميلادي،
        الأشهر_الميلادية(الشهر_الميلادي - 1)،
        السنة_الميلادية
    )؛

    عرف نص_هجري: نـص؛
    نص_هجري = نـص.املأ("%i %s %i هـ"،
        تاريخ_هجري.اليوم،
        الأشهر_الهجرية(تاريخ_هجري.الشهر - 1)،
        تاريخ_هجري.السنة
    )؛

    // حساب المناسبات الهجرية القادمة
    عرف المناسبات: مـصفوفة[مـناسبة] = هات_المناسبات_الهجرية_القادمة(تاريخ_هجري)؛

    // متغيرات للمحول
    عرف مدخل_يوم: سـندنا[مـدخل]؛
    عرف مدخل_شهر: سـندنا[مـدخل]؛
    عرف مدخل_سنة: سـندنا[مـدخل]؛
    عرف نتيجة_التحويل: سـندنا[كـتابة]؛
    عرف زر_م_إلى_هـ: سـندنا[الـزر_الانتقائي]؛
    عرف زر_هـ_إلى_م: سـندنا[الـزر_الانتقائي]؛

    نـافذة.النموذج.الطراز.{
        الحشوة = مـسافة4.بكسلات(0)؛
        الهامش = مـسافة4.بكسلات(0)؛
        أدنى_طول = مـسافة.مئوي(100)؛
        الخلفية = خـلفية(لـون("E0E0F5"))؛
        فئة_الخط = "Tajawal"؛
        الاتجاه = اتـجاه._من_اليمين_؛
    }؛

    نـافذة.النموذج.حدد_المشهد(صـندوق().{
        الطراز.{
            الإظهار = إظـهار._مرن_؛
            النسق = نـسق._عمود_؛
            المحاذاة = مـحاذاة._وسط_؛
            ملء_السطر = مـلء_سطر._بداية_؛
            الهامش = مـسافة4.بكسلات(20، 0، 20، 0)؛
            الفجوة = مـسافة.بكسلات(30)؛
        }؛
        
        أضف_فروع({
            صـندوق().{
                الطراز.{
                    الإظهار = إظـهار._مرن_؛
                    النسق = نـسق._سطر_؛
                    لف_المرونة = لـف_مرونة._لف_؛
                    المحاذاة = مـحاذاة._بداية_؛
                    ملء_السطر = مـلء_سطر._وسط_؛
                    الفجوة = مـسافة.بكسلات(30)؛
                }؛
                أضف_فروع({
                    صـندوق().{
                        الطراز.{
                            الخلفية = خـلفية(لـون("2c3e50"))؛
                            الحشوة = مـسافة4.بكسلات(40)؛
                            الإظهار = إظـهار._مرن_؛
                            النسق = نـسق._عمود_؛
                            المحاذاة = مـحاذاة._وسط_؛
                            نصف_قطر_الإطار = مـسافة4.بكسلات(10)؛
                            ظل_الصندوق = ظـل_صندوق(
                                مـسافة.بكسلات(4)،
                                مـسافة.بكسلات(6)،
                                مـسافة.بكسلات(15)،
                                مـسافة.بكسلات(2)،
                                لـون(0، 0، 0، 25)
                            )؛
                        }؛
                        أضف_فروع({
                            صـورة().{
                                الرابط = نـص("/hijri_icon_64.png")؛
                                الطراز.العرض = مـسافة.بكسلات(64)؛
                            }،
                            كـتابة(نـص("هجري")).{
                                الطراز.{
                                    لون_الخط = لـون("ecf0f1")؛
                                    حجم_الخط = مـسافة.بكسلات(48.0)؛
                                    فئة_الخط = "TajawalBold"؛
                                    سمك_الخط = 700؛
                                    محاذاة_النص = مـحاذاة_نص._وسط_؛
                                    الهامش = مـسافة4.بكسلات(0، 0، 30، 0)؛
                                }
                            }،
                            كـتابة(يوم_الأسبوع).{
                                الطراز.{
                                    لون_الخط = لـون("44C8FBFF")؛
                                    حجم_الخط = مـسافة.بكسلات(28.0)؛
                                    فئة_الخط = "TajawalBold"؛
                                    سمك_الخط = 600؛
                                    محاذاة_النص = مـحاذاة_نص._وسط_؛
                                    الهامش = مـسافة4.بكسلات(0، 0، 20، 0)؛
                                }
                            }،
                            كـتابة(نص_هجري).{
                                الطراز.{
                                    لون_الخط = لـون("ecf0f1")؛
                                    حجم_الخط = مـسافة.بكسلات(20.0)؛
                                    محاذاة_النص = مـحاذاة_نص._وسط_؛
                                    الهامش = مـسافة4.بكسلات(0، 0، 10، 0)؛
                                }
                            }،
                            كـتابة(نص_ميلادي).{
                                الطراز.{
                                    لون_الخط = لـون("ecf0f1")؛
                                    حجم_الخط = مـسافة.بكسلات(20.0)؛
                                    محاذاة_النص = مـحاذاة_نص._وسط_؛
                                }
                            }
                        })؛
                    }،

                    // صندوق محول التواريخ
                    صـندوق().{
                        الطراز.{
                            الخلفية = خـلفية(لـون("ffffff"))؛
                            الحشوة = مـسافة4.بكسلات(30)؛
                            الإظهار = إظـهار._مرن_؛
                            النسق = نـسق._عمود_؛
                            نصف_قطر_الإطار = مـسافة4.بكسلات(10)؛
                            ظل_الصندوق = ظـل_صندوق(
                                مـسافة.بكسلات(4)،
                                مـسافة.بكسلات(6)،
                                مـسافة.بكسلات(15)،
                                مـسافة.بكسلات(2)،
                                لـون(0، 0، 0، 25)
                            )؛
                            أقصى_عرض = مـسافة.بكسلات(600)؛
                        }؛

                        أضف_فروع({
                            // عنوان المحول
                            كـتابة(نـص("محول التواريخ")).{
                                الطراز.{
                                    لون_الخط = لـون("2c3e50")؛
                                    حجم_الخط = مـسافة.بكسلات(24.0)؛
                                    فئة_الخط = "TajawalBold"؛
                                    سمك_الخط = 700؛
                                    محاذاة_النص = مـحاذاة_نص._وسط_؛
                                    الهامش = مـسافة4.بكسلات(0، 0، 15، 0)؛
                                }
                            }،

                            // اختيار اتجاه التحويل
                            صـندوق().{
                                الطراز.{
                                    الإظهار = إظـهار._مرن_؛
                                    النسق = نـسق._عمود_؛
                                    الهامش = مـسافة4.بكسلات(0، 0، 15، 0)؛
                                }؛
                                أضف_فروع({
                                    كـتابة(نـص("اتجاه التحويل:")).{
                                        الطراز.{
                                            لون_الخط = لـون("2c3e50")؛
                                            حجم_الخط = مـسافة.بكسلات(14.0)؛
                                            فئة_الخط = "TajawalBold"؛
                                            سمك_الخط = 600؛
                                            الهامش = مـسافة4.بكسلات(0، 0، 10، 0)؛
                                        }
                                    }،
                                    صـندوق().{
                                        الطراز.{
                                            الإظهار = إظـهار._مرن_؛
                                            النسق = نـسق._عمود_؛
                                            الفجوة = مـسافة.بكسلات(8)؛
                                        }؛
                                        أضف_فروع({
                                            صـندوق().{
                                                الطراز.{
                                                    الإظهار = إظـهار._مرن_؛
                                                    النسق = نـسق._سطر_؛
                                                    المحاذاة = مـحاذاة._وسط_؛
                                                    الفجوة = مـسافة.بكسلات(8)؛
                                                }؛
                                                أضف_فروع({
                                                    الـزر_الانتقائي(نـص("اتجاه_التحويل")، نـص("من_ميلادي_إلى_هجري")).{
                                                        زر_م_إلى_هـ = هذا؛
                                                        محدد = 1؛
                                                    }،
                                                    كـتابة(نـص("ميلادي ← هجري")).{
                                                        الطراز.{
                                                            حجم_الخط = مـسافة.بكسلات(14.0)؛
                                                            لون_الخط = لـون("2c3e50")؛
                                                        }
                                                    }
                                                })؛
                                            }،
                                            صـندوق().{
                                                الطراز.{
                                                    الإظهار = إظـهار._مرن_؛
                                                    النسق = نـسق._سطر_؛
                                                    المحاذاة = مـحاذاة._وسط_؛
                                                    الفجوة = مـسافة.بكسلات(8)؛
                                                }؛
                                                أضف_فروع({
                                                    الـزر_الانتقائي(نـص("اتجاه_التحويل")، نـص("من_هجري_إلى_ميلادي")).{
                                                        زر_هـ_إلى_م = هذا؛
                                                    }،
                                                    كـتابة(نـص("هجري ← ميلادي")).{
                                                        الطراز.{
                                                            حجم_الخط = مـسافة.بكسلات(14.0)؛
                                                            لون_الخط = لـون("2c3e50")؛
                                                        }
                                                    }
                                                })؛
                                            }
                                        })؛
                                    }
                                })؛
                            }،

                            // واجهة التحويل الموحدة
                            صـندوق().{
                                الطراز.{
                                    الإظهار = إظـهار._مرن_؛
                                    النسق = نـسق._عمود_؛
                                    الحشوة = مـسافة4.بكسلات(15)؛
                                    الخلفية = خـلفية(لـون("ecf0f1"))؛
                                    نصف_قطر_الإطار = مـسافة4.بكسلات(8)؛
                                }؛
                                أضف_فروع({
                                    // صف لحقول الإدخال
                                    صـندوق().{
                                        الطراز.{
                                            الإظهار = إظـهار._مرن_؛
                                            النسق = نـسق._سطر_؛
                                            الفجوة = مـسافة.بكسلات(10)؛
                                            الهامش = مـسافة4.بكسلات(0، 0، 10، 0)؛
                                        }؛
                                        أضف_فروع({
                                            صـندوق().{
                                                الطراز.{
                                                    الإظهار = إظـهار._مرن_؛
                                                    النسق = نـسق._عمود_؛
                                                    المرونة = مـرونة(1)؛
                                                }؛
                                                أضف_فروع({
                                                    كـتابة(نـص("اليوم")).{
                                                        الطراز.{
                                                            حجم_الخط = مـسافة.بكسلات(12.0)؛
                                                            لون_الخط = لـون("7f8c8d")؛
                                                            الهامش = مـسافة4.بكسلات(0، 0، 5، 0)؛
                                                        }
                                                    }،
                                                    مـدخل().{
                                                        مدخل_يوم = هذا؛
                                                        الطراز.{
                                                            الحشوة = مـسافة4.بكسلات(10)؛
                                                            حجم_الخط = مـسافة.بكسلات(14.0)؛
                                                            نصف_قطر_الإطار = مـسافة4.بكسلات(5)؛
                                                            سمك_الإطار = مـسافة4.بكسلات(1)؛
                                                            لون_الإطار = لـون("bdc3c7")؛
                                                            العرض = مـسافة.مئوي(100)؛
                                                        }؛
                                                        قيمة_وصفية = نـص("5")؛
                                                    }
                                                })؛
                                            }،
                                            صـندوق().{
                                                الطراز.{
                                                    الإظهار = إظـهار._مرن_؛
                                                    النسق = نـسق._عمود_؛
                                                    المرونة = مـرونة(1)؛
                                                }؛
                                                أضف_فروع({
                                                    كـتابة(نـص("الشهر")).{
                                                        الطراز.{
                                                            حجم_الخط = مـسافة.بكسلات(12.0)؛
                                                            لون_الخط = لـون("7f8c8d")؛
                                                            الهامش = مـسافة4.بكسلات(0، 0، 5، 0)؛
                                                        }
                                                    }،
                                                    مـدخل().{
                                                        مدخل_شهر = هذا؛
                                                        الطراز.{
                                                            الحشوة = مـسافة4.بكسلات(10)؛
                                                            حجم_الخط = مـسافة.بكسلات(14.0)؛
                                                            نصف_قطر_الإطار = مـسافة4.بكسلات(5)؛
                                                            سمك_الإطار = مـسافة4.بكسلات(1)؛
                                                            لون_الإطار = لـون("bdc3c7")؛
                                                            العرض = مـسافة.مئوي(100)؛
                                                        }؛
                                                        قيمة_وصفية = نـص("12")؛
                                                    }
                                                })؛
                                            }،
                                            صـندوق().{
                                                الطراز.{
                                                    الإظهار = إظـهار._مرن_؛
                                                    النسق = نـسق._عمود_؛
                                                    المرونة = مـرونة(1.5)؛
                                                }؛
                                                أضف_فروع({
                                                    كـتابة(نـص("السنة")).{
                                                        الطراز.{
                                                            حجم_الخط = مـسافة.بكسلات(12.0)؛
                                                            لون_الخط = لـون("7f8c8d")؛
                                                            الهامش = مـسافة4.بكسلات(0، 0، 5، 0)؛
                                                        }
                                                    }،
                                                    مـدخل().{
                                                        مدخل_سنة = هذا؛
                                                        الطراز.{
                                                            الحشوة = مـسافة4.بكسلات(10)؛
                                                            حجم_الخط = مـسافة.بكسلات(14.0)؛
                                                            نصف_قطر_الإطار = مـسافة4.بكسلات(5)؛
                                                            سمك_الإطار = مـسافة4.بكسلات(1)؛
                                                            لون_الإطار = لـون("bdc3c7")؛
                                                            العرض = مـسافة.مئوي(100)؛
                                                        }؛
                                                        قيمة_وصفية = نـص("2025")؛
                                                    }
                                                })؛
                                            }
                                        })؛
                                    }،
                                    // زر التحويل
                                    الـزر(نـص("تحويل ←")).{
                                        الطراز.{
                                            الخلفية = خـلفية(لـون("27ae60"))؛
                                            لون_الخط = لـون("ffffff")؛
                                            الحشوة = مـسافة4.بكسلات(12، 24، 12، 24)؛
                                            نصف_قطر_الإطار = مـسافة4.بكسلات(5)؛
                                            حجم_الخط = مـسافة.بكسلات(16.0)؛
                                            فئة_الخط = "TajawalBold"؛
                                            سمك_الخط = 600؛
                                            الهامش = مـسافة4.بكسلات(0، 0، 10، 0)؛
                                            المؤشر = مـؤشر._سبابة_؛
                                        }؛
                                        عند_الضغط.اربط(مغلفة (مدخل_يوم: كسند، مدخل_شهر: كسند، مدخل_سنة: كسند، نتيجة_التحويل: كسند، زر_م_إلى_هـ: كسند)&(سند[ودجـة]، سند[صـحيح]) {
                                            عرف يوم: صـحيح = نـص.اقرأ_صحيح(مدخل_يوم.النص)؛
                                            عرف شهر: صـحيح = نـص.اقرأ_صحيح(مدخل_شهر.النص)؛
                                            عرف سنة: صـحيح = نـص.اقرأ_صحيح(مدخل_سنة.النص)؛

                                            إذا زر_م_إلى_هـ.محدد {
                                                // من ميلادي إلى هجري
                                                عرف يوم_اسبوع: نـص = احسب_يوم_الاسبوع(سنة، شهر، يوم)؛
                                                عرف تاريخ: هـجري.تـاريخ = هـجري.حول_لهجري(سنة، شهر، يوم)؛
                                                نتيجة_التحويل.النص = نـص.املأ("%s، %i %s %i هـ"،
                                                    يوم_اسبوع.صوان،
                                                    تاريخ.اليوم،
                                                    الأشهر_الهجرية(تاريخ.الشهر - 1)،
                                                    تاريخ.السنة
                                                )؛
                                            } وإلا {
                                                // من هجري إلى ميلادي
                                                عرف تاريخ: هـجري.تـاريخ = هـجري.حول_لميلادي(سنة، شهر، يوم)؛
                                                عرف يوم_اسبوع: نـص = احسب_يوم_الاسبوع(تاريخ.السنة، تاريخ.الشهر، تاريخ.اليوم)؛
                                                نتيجة_التحويل.النص = نـص.املأ("%s، %i %s %i م"،
                                                    يوم_اسبوع.صوان،
                                                    تاريخ.اليوم،
                                                    الأشهر_الميلادية(تاريخ.الشهر - 1)،
                                                    تاريخ.السنة
                                                )؛
                                            }
                                        })؛
                                    }،
                                    // عرض النتيجة
                                    كـتابة(نـص(" --- ")).{
                                        نتيجة_التحويل = هذا؛
                                        الطراز.{
                                            لون_الخط = لـون("27ae60")؛
                                            حجم_الخط = مـسافة.بكسلات(16.0)؛
                                            فئة_الخط = "TajawalBold"؛
                                            سمك_الخط = 600؛
                                            محاذاة_النص = مـحاذاة_نص._وسط_؛
                                        }
                                    }
                                })؛
                            }
                        })؛
                    }،

                    // صندوق المناسبات الهجرية القادمة
                    صـندوق().{
                        الطراز.{
                            الخلفية = خـلفية(لـون("ffffff"))؛
                            الحشوة = مـسافة4.بكسلات(30)؛
                            الإظهار = إظـهار._مرن_؛
                            النسق = نـسق._عمود_؛
                            المحاذاة = مـحاذاة._وسط_؛
                            نصف_قطر_الإطار = مـسافة4.بكسلات(10)؛
                            ظل_الصندوق = ظـل_صندوق(
                                مـسافة.بكسلات(4)،
                                مـسافة.بكسلات(6)،
                                مـسافة.بكسلات(15)،
                                مـسافة.بكسلات(2)،
                                لـون(0، 0، 0، 25)
                            )؛
                            أقصى_عرض = مـسافة.بكسلات(800)؛
                        }؛

                        أضف_فروع({
                            // عنوان المناسبات
                            كـتابة(نـص("المناسبات الهجرية القادمة")).{
                                الطراز.{
                                    لون_الخط = لـون("2c3e50")؛
                                    حجم_الخط = مـسافة.بكسلات(24.0)؛
                                    فئة_الخط = "TajawalBold"؛
                                    سمك_الخط = 700؛
                                    محاذاة_النص = مـحاذاة_نص._وسط_؛
                                    الهامش = مـسافة4.بكسلات(0، 0، 20، 0)؛
                                }
                            }،
                            // إضافة كل مناسبة
                            صـندوق().{
                                الطراز.{
                                    الحشوة = مـسافة4.بكسلات(20، 0، 0، 0)؛
                                    الإظهار = إظـهار._مرن_؛
                                    النسق = نـسق._سطر_؛
                                    لف_المرونة = لـف_مرونة._لف_؛
                                    ملء_السطر = مـلء_سطر._وسط_؛
                                    الفجوة = مـسافة.بكسلات(20)؛
                                }؛
                                عرف ع: صـحيح؛
                                لكل ع = 0، ع < المناسبات.هات_الطول()، ++ع {
                                    عرف مناسبة: مـناسبة = المناسبات(ع)؛
                                    أضف_فروع({ صـندوق_مناسبة(مناسبة) })؛
                                }
                            }
                        })؛
                    }،
                })؛
            }،
            كـتابة(نـص("حساب التواريخ الهجرية يعتمد على خوارزمية أم القرى")).{
                الطراز.{
                    لون_الخط = لـون("34495e")؛
                    حجم_الخط = مـسافة.بكسلات(14.0)؛
                }؛
            }،

            // رابط GitHub
            صـندوق().{
                الطراز.{
                    الإظهار = إظـهار._مرن_؛
                    النسق = نـسق._سطر_؛
                    المحاذاة = مـحاذاة._وسط_؛
                    ملء_السطر = مـلء_سطر._وسط_؛
                    الفجوة = مـسافة.بكسلات(8)؛
                    لون_الخط = لـون("2c3e50")؛
                }؛
                أضف_فروع({
                    ارتـباط_تشعبي().{
                        العنوان = نـص("https://github.com/sarmadka/Hijri")؛
                        لسان_جديد = 1؛
                        حدد_الفرع(
                            صـورة().{
                                الرابط = نـص("https://cdn.simpleicons.org/github/2c3e50")؛
                                الطراز.{
                                    العرض = مـسافة.بكسلات(24)؛
                                    الطول = مـسافة.بكسلات(24)؛
                                }؛
                            }،
                        )؛
                    }،
                    ارتـباط_تشعبي().{
                        العنوان = نـص("https://github.com/sarmadka/Hijri")؛
                        لسان_جديد = 1؛
                        الطراز.زخرفة_النص = زخـرفة_نص._بلا_؛
                        حدد_الفرع(
                            كـتابة(نـص("ساهم في التطوير")).{
                                الطراز.{
                                    حجم_الخط = مـسافة.بكسلات(16.0)؛
                                    لون_الخط = لـون("2c3eA0")؛
                                }
                            }
                        )؛
                    }
                })؛
            }،
        })؛
    })؛

    نفذ_حلقة_معالجة_الأحداث()؛
}

//=============================================================================
// بدء الخادم

نـاشر.إطناب = 0؛
نـاشر.اضبط[نـاشر.إعـدادات().{
    اسم_المشروع = "هجري"؛
    رقم_الإصدار = "v1"؛
    منفذ_الخادم = 8000؛
    مشغل_النشر = نـاشر.مـشغل_نشر_الأسس_نت(نـص("hijri"))؛
}]؛
بـروجارج.حلل(2)؛
